-- Naming Style
    -- _u = util
    -- _c = class
    -- _r = reference
    -- _t = tab
    -- _d = default
    -- _i = interface
    -- _l = call
    -- _e = entity
    -- _s = class
    -- _p = property
    -- _f = font
    -- _n = number
    -- _x = context
    -- _fl = flag
    -- _bt = button
    -- _jt = jumptype

macro varname = (s) ->
    "#{s}\\gsub('%s+', '.')\\gsub('-+', '.')\\gsub('%.%+', '.')\\lower!"

macro is_blank = (s) ->
    "#{s} == nil or #(#{s}\\gsub('%s+', '')) == 0"

macro is_empty = (t) ->
    "#{t} == nil or #(#{t}) == 0"

macro ref = (...) ->
    "gui.Reference #{...}"



local _u =
    fnext: (ft, fn, fs, fw) ->
        if ft == nil
            return nil

        fn = tostring fn
        fs = tostring fs
        fw = tostring fw

        if ft[fn] == nil
            ft[fn] = {}
        if ft[fn][fs] == nil
            ft[fn][fs] = {}
        if ft[fn][fs][fw] == nil
            ft[fn][fs][fw] = draw.CreateFont fn, (tonumber fs), tonumber fw

        ft[fn][fs][fw]


    rgb2hsb: (r, g, b, a) ->
        r = if r then r / 255 else 0
        g = if g then g / 255 else 0
        b = if b then b / 255 else 0
        a = if a then a / 255 else 0

        r = math.max 0, math.min 1, r
        g = math.max 0, math.min 1, g
        b = math.max 0, math.min 1, b
        a = math.max 0, math.min 1, a

        min = math.min r, g, b
        max = math.max r, g, b

        brightness = max
        saturation = if max ~= 0 then (max - min) / max else 0

        hue = 0
        if max ~= min
            if max == r
                hue = (g - b) / (max - min)
            elseif max == g
                hue = 2 + (b - r) / (max - min)
            else
                hue = 4 + (r - g) / (max - min)

        hue /= 6
        if hue < 0
            hue += 1
        elseif hue > 1
            hue -= 1

        hue, saturation, brightness, a


    hsb2rgb: (h, s, b, a) ->
        h = if h then h else 0
        s = if s then s else 0
        b = if b then b else 0
        a = if a then a else 1

        h = math.max 0, math.min 1, h
        s = math.max 0, math.min 1, s
        b = math.max 0, math.min 1, b
        a = math.max 0, math.min 1, a

        hue = h * 360
        if s == 0
            v = math.floor b * 255 + 0.5
            return v, v, v, math.floor a * 255 + 0.5

        sector = math.floor hue / 60
        fraction = (hue / 60) - sector

        p = b * (1 - s)
        q = b * (1 - s * fraction)
        t = b * (1 - s * (1 - fraction))

        red = green = blue = 0
        if sector == 0
            red, green, blue = b, t, p
        elseif sector == 1
            red, green, blue = q, b, p
        elseif sector == 2
            red, green, blue = p, b, t
        elseif sector == 3
            red, green, blue = p, q, b
        elseif sector == 4
            red, green, blue = t, p, b
        else
            red, green, blue = b, p, q

        (math.floor red * 255 + 0.5), (math.floor green * 255 + 0.5), (math.floor blue * 255 + 0.5), math.floor a * 255 + 0.5


    vlen3: (x, y, z) ->
        x = tonumber x
        y = tonumber y
        z = tonumber z

        math.sqrt x^2 + y^2 + z^2



local _c =
    BaseBuilder: class
        new: =>
            @_parent = nil
            @_name = nil
            @_varname = nil
            @_description = nil
            @_default = nil

        parent: (parent) =>
            @_parent = parent
            @

        name: (name) =>
            @_name = name
            @

        varname: (varname) =>
            @_varname = varname
            @

        description: (description) =>
            @_description = description
            @

        default: (default) =>
            @_default = default
            @

        __tostring: =>
            @_varname .. '("' .. @_name .. '")'



_c.CheckBuilder = class extends _c.BaseBuilder
    new: =>
        @_default = false

    build: =>
        if $is_blank @_name
            error 'Name cannot be blank for a checkbox'
        if $is_blank @_varname
            @_varname = ($varname @_name) .. '.checkbox'

        element = gui.Checkbox @_parent, @_varname, @_name, @_default
        unless $is_blank @_description
            element\SetDescription @_description

        element



_c.ColorBuilder = class extends _c.BaseBuilder
    build: =>
        if $is_blank @_name
            error 'Name cannot be blank for a color picker'
        unless $is_blank @_description
            error 'Description is disallowed for a color picker'
        if $is_empty @_default
            error 'Default value cannot be empty for a color picker'
        if $is_blank @_varname
            @_varname = ($varname @_name) .. '.colorpicker'

        gui.ColorPicker @_parent, @_varname, @_name, unpack(@_default)



_c.EditBuilder = class extends _c.BaseBuilder
    build: =>
        if $is_blank @_name
            error 'Name cannot be blank for an editbox'
        if $is_blank @_varname
            @_varname = ($varname @_name) .. '.editbox'

        element = gui.Editbox @_parent, @_varname, @_name
        unless $is_blank @_description
            element\SetDescription @_description

        element



_c.ComboBuilder = class extends _c.BaseBuilder
    build: =>
        if $is_blank @_name
            error 'Name cannot be blank for a combobox'
        if $is_empty @_default
            error 'Default value cannot be empty for a combobox'
        if $is_blank @_varname
            @_varname = ($varname @_name) .. '.combobox'

        element = gui.Combobox @_parent, @_varname, @_name, unpack(@_default)
        unless $is_blank @_description
            element\SetDescription @_description

        element



_c.SliderBuilder = class extends _c.BaseBuilder
    new: =>
        @_min = nil
        @_max = nil
        @_step = nil

    min: (min) =>
        @_min = min
        @

    max: (max) =>
        @_max = max
        @

    step: (step) =>
        @_step = step
        @

    build: =>
        if $is_blank @_name
            error 'Name cannot be blank for a slider'
        if @_default == nil
            error 'Default value cannot be nil for a slider'
        if @_min == nil
            error 'Minimum value cannot be nil for a slider'
        if @_max == nil
            error 'Maximum value cannot be nil for a slider'
        if @_step == nil
            error 'Step cannot be nil for a slider'
        if @_min >= @_max
            error 'Minimum value should be less than the maximum value for a slider'
        if $is_blank @_varname
            @_varname = ($varname @_name) .. '.slider'

        element = gui.Slider @_parent, @_varname, @_name, @_default, @_min, @_max, @_step
        unless $is_blank @_description
            element\SetDescription @_description

        element



_c.TabBuilder = class extends _c.BaseBuilder
    build: =>
        if @_parent == nil
            error 'Parent cannot be nil for a tab'
        if $is_blank @_name
            error 'Name cannot be blank for a tab'
        if $is_blank @_varname
            @_varname = $varname ((tostring @_parent) .. ' ' .. @_name)

        gui.Tab @_parent, @_varname, @_name



local _r =
    menu: $ref 'Menu'
    settings: $ref 'Settings'



local _t =
    movement: _c.TabBuilder!
        \parent _r.settings
        \name 'Movement'
        \build!



local _d =
    font:
        names: ['Consolas', 'AcPlus IBM BIOS']

        size:
            min: 1
            max: 64
            step: 1
            default: 20

        weight:
            min: 100
            max: 900
            step: 100
            default: 900

    speed:
        position:
            x: 0.5
            y: 0.4
            min: 0
            max: 1
            step: 0.01

        accuracy:
            min: 0
            max: 3
            step: 1
            default: 0

        zeros:
            min: 0
            max: 3
            step: 1
            default: 0

    shadow:
        offset:
            x: 1
            y: 1
            min: -2
            max: 2
            step: 1

    color:
        debug: [0, 255, 0, 255]
        shadow: [0, 0, 0, 255]
        speed: [0, 255, 0, 255]

        saturation:
            min: 0
            max: 1
            step: 0.01
            default: 1

        brightness:
            min: 0
            max: 1
            step: 0.01
            default: 1



local _i =
    global_font_check: _c.CheckBuilder!
        \parent _t.movement
        \name 'Global Font'
        \description 'Render any supported text using the global font.'
        \default true
        \build!

    global_font_name_combo: _c.ComboBuilder!
        \parent _t.movement
        \name 'Global Font Name'
        \description 'Set the global font name used to render any text.'
        \default _d.font.names
        \build!

    global_font_size_slider: _c.SliderBuilder!
        \parent _t.movement
        \name 'Global Font Size'
        \description 'Set the global font size used to render any text.'
        \default _d.font.size.default
        \min _d.font.size.min
        \max _d.font.size.max
        \step _d.font.size.step
        \build!

    global_font_weight_slider: _c.SliderBuilder!
        \parent _t.movement
        \name 'Global Font Weight'
        \description 'Set the global font weight used to render any text.'
        \default _d.font.weight.default
        \min _d.font.weight.min
        \max _d.font.weight.max
        \step _d.font.weight.step
        \build!

    global_shadow_check: _c.CheckBuilder!
        \parent _t.movement
        \name 'Global Shadow'
        \description 'Render shadows of all elements that are supported. '
        \default true
        \build!

    global_shadow_offset_x_slider: _c.SliderBuilder!
        \parent _t.movement
        \name 'Global Shadow Offset X-Axis'
        \description 'Set the global shadow offset on the x-axis.'
        \default _d.shadow.offset.x
        \min _d.shadow.offset.min
        \max _d.shadow.offset.max
        \step _d.shadow.offset.step
        \build!

    global_shadow_offset_y_slider: _c.SliderBuilder!
        \parent _t.movement
        \name 'Global Shadow Offset Y-Axis'
        \description 'Set the global shadow offset on the y-axis.'
        \default _d.shadow.offset.y
        \min _d.shadow.offset.min
        \max _d.shadow.offset.max
        \step _d.shadow.offset.step
        \build!

    global_shadow_color: _c.ColorBuilder!
        \parent _t.movement
        \name 'Global Shadow Color'
        \default _d.color.shadow
        \build!

    global_shadow_saturation_slider: _c.SliderBuilder!
        \parent _t.movement
        \name 'Global Shadow Color Saturation'
        \description 'Set the global shadow color saturation.'
        \default _d.color.saturation.default
        \min _d.color.saturation.min
        \max _d.color.saturation.max
        \step _d.color.saturation.step
        \build!

    global_shadow_brightness_slider: _c.SliderBuilder!
        \parent _t.movement
        \name 'Global Shadow Color Brightness'
        \description 'Set the global shadow color brightness.'
        \default _d.color.brightness.default
        \min _d.color.brightness.min
        \max _d.color.brightness.max
        \step _d.color.brightness.step
        \build!

    debug_check: _c.CheckBuilder!
        \parent _t.movement
        \name 'Debug'
        \description 'Show debug text on the screen.'
        \build!

    debug_spectators_check: _c.CheckBuilder!
        \parent _t.movement
        \name 'Debug Spectators'
        \description 'Show the list of spectators in the top-left corner of the screen.'
        \build!

    debug_spectators_bot_check: _c.CheckBuilder!
        \parent _t.movement
        \name 'Debug Spectators Bot'
        \description 'Switch to show or hide bots from the list of spectators.'
        \build!

    debug_spectators_self_check: _c.CheckBuilder!
        \parent _t.movement
        \name 'Debug Spectators Self'
        \description 'Switch to show or hide yourself from the list of spectators.'
        \build!

    debug_jt_check: _c.CheckBuilder!
        \parent _t.movement
        \name 'Debug Jump Type'
        \description 'Show debug text on the top-right corner of the screen with current jump type.'
        \build!

    debug_color: _c.ColorBuilder!
        \parent _t.movement
        \name 'Debug Color'
        \default _d.color.debug
        \build!

    debug_saturation_slider: _c.SliderBuilder!
        \parent _t.movement
        \name 'Debug Color Saturation'
        \description 'Set the debug color saturation.'
        \default _d.color.saturation.default
        \min _d.color.saturation.min
        \max _d.color.saturation.max
        \step _d.color.saturation.step
        \build!

    debug_brightness_slider: _c.SliderBuilder!
        \parent _t.movement
        \name 'Debug Color Brightness'
        \description 'Set the debug color brightness.'
        \default _d.color.brightness.default
        \min _d.color.brightness.min
        \max _d.color.brightness.max
        \step _d.color.brightness.step
        \build!

    debug_font_check: _c.CheckBuilder!
        \parent _t.movement
        \name 'Debug Font'
        \description 'Override the global font parameters.'
        \build!

    debug_font_name_combo: _c.ComboBuilder!
        \parent _t.movement
        \name 'Debug Font Name'
        \description 'Set the debug font name used to render debug text.'
        \default _d.font.names
        \build!

    debug_font_size_slider: _c.SliderBuilder!
        \parent _t.movement
        \name 'Debug Font Size'
        \description 'Set the debug font size used to render debug text.'
        \default _d.font.size.default
        \min _d.font.size.min
        \max _d.font.size.max
        \step _d.font.size.step
        \build!

    debug_font_weight_slider: _c.SliderBuilder!
        \parent _t.movement
        \name 'Debug Font Weight'
        \description 'Set the debug font weight used to render debug text.'
        \default _d.font.weight.default
        \min _d.font.weight.min
        \max _d.font.weight.max
        \step _d.font.weight.step
        \build!

    debug_shadow_check: _c.CheckBuilder!
        \parent _t.movement
        \name 'Debug Shadow'
        \description 'Override the global shadow parameters.'
        \build!

    debug_shadow_offset_x_slider: _c.SliderBuilder!
        \parent _t.movement
        \name 'Debug Shadow Offset X-Axis'
        \description 'Set the debug shadow offset on the x-axis.'
        \default _d.shadow.offset.x
        \min _d.shadow.offset.min
        \max _d.shadow.offset.max
        \step _d.shadow.offset.step
        \build!

    debug_shadow_offset_y_slider: _c.SliderBuilder!
        \parent _t.movement
        \name 'Debug Shadow Offset Y-Axis'
        \description 'Set the debug shadow offset on the y-axis.'
        \default _d.shadow.offset.y
        \min _d.shadow.offset.min
        \max _d.shadow.offset.max
        \step _d.shadow.offset.step
        \build!

    debug_shadow_color: _c.ColorBuilder!
        \parent _t.movement
        \name 'Debug Shadow Color'
        \default _d.color.shadow
        \build!

    debug_shadow_saturation_slider: _c.SliderBuilder!
        \parent _t.movement
        \name 'Debug Shadow Color Saturation'
        \description 'Set the debug shadow color saturation.'
        \default _d.color.saturation.default
        \min _d.color.saturation.min
        \max _d.color.saturation.max
        \step _d.color.saturation.step
        \build!

    debug_shadow_brightness_slider: _c.SliderBuilder!
        \parent _t.movement
        \name 'Debug Shadow Color Brightness'
        \description 'Set the debug shadow color brightness.'
        \default _d.color.brightness.default
        \min _d.color.brightness.min
        \max _d.color.brightness.max
        \step _d.color.brightness.step
        \build!

    speed_check: _c.CheckBuilder!
        \parent _t.movement
        \name 'Speed'
        \description 'Show player\'s speed on the screen.'
        \build!

    speed_hide_check: _c.CheckBuilder!
        \parent _t.movement
        \name 'Speed Hide'
        \description 'Hide player\'s speed while scoping.'
        \build!

    speed_ladder_check: _c.CheckBuilder!
        \parent _t.movement
        \name 'Speed Ladder'
        \description 'Show player\'s vertical speed while ladder moving.'
        \build!

    speed_color: _c.ColorBuilder!
        \parent _t.movement
        \name 'Speed Color'
        \default _d.color.speed
        \build!

    speed_saturation_slider: _c.SliderBuilder!
        \parent _t.movement
        \name 'Speed Color Saturation'
        \description 'Set the speed color saturation.'
        \default _d.color.saturation.default
        \min _d.color.saturation.min
        \max _d.color.saturation.max
        \step _d.color.saturation.step
        \build!

    speed_brightness_slider: _c.SliderBuilder!
        \parent _t.movement
        \name 'Speed Color Brightness'
        \description 'Set the speed color brightness.'
        \default _d.color.brightness.default
        \min _d.color.brightness.min
        \max _d.color.brightness.max
        \step _d.color.brightness.step
        \build!

    speed_x_check: _c.CheckBuilder!
        \parent _t.movement
        \name 'Speed By X-Axis'
        \description 'Use player\'s x-axis speed for accumulating.'
        \default true
        \build!

    speed_y_check: _c.CheckBuilder!
        \parent _t.movement
        \name 'Speed By Y-Axis'
        \description 'Use player\'s y-axis speed for accumulating.'
        \default true
        \build!

    speed_z_check: _c.CheckBuilder!
        \parent _t.movement
        \name 'Speed By Z-AXis'
        \description 'Use player\'s z-axis speed for accumulating.'
        \build!

    speed_x_pos_slider: _c.SliderBuilder!
        \parent _t.movement
        \name 'Speed X-Axis Position'
        \description 'Set player\'s speed position on the x-axis of the screen.'
        \default _d.speed.position.x
        \min _d.speed.position.min
        \max _d.speed.position.max
        \step _d.speed.position.step
        \build!

    speed_y_pos_slider: _c.SliderBuilder!
        \parent _t.movement
        \name 'Speed Y-Axis Position'
        \description 'Set player\'s speed position on the y-axis of the screen.'
        \default _d.speed.position.y
        \min _d.speed.position.min
        \max _d.speed.position.max
        \step _d.speed.position.step
        \build!

    speed_acc_slider: _c.SliderBuilder!
        \parent _t.movement
        \name 'Speed Accuracy'
        \description 'Set player\'s speed accuracy which represents the number of digits after the value dot.'
        \default _d.speed.accuracy.default
        \min _d.speed.accuracy.min
        \max _d.speed.accuracy.max
        \step _d.speed.accuracy.step
        \build!

    speed_zeros_slider: _c.SliderBuilder!
        \parent _t.movement
        \name 'Speed Zeros'
        \description 'Set player\'s speed zeros wich represents the number of leading zeros of the values.'
        \default _d.speed.zeros.default
        \min _d.speed.zeros.min
        \max _d.speed.zeros.max
        \step _d.speed.zeros.step
        \build!

    speed_font_check: _c.CheckBuilder!
        \parent _t.movement
        \name 'Speed Font'
        \description 'Override the global font parameters.'
        \build!

    speed_font_name_combo: _c.ComboBuilder!
        \parent _t.movement
        \name 'Speed Font Name'
        \description 'Set the speed font name used to render speed text.'
        \default _d.font.names
        \build!

    speed_font_size_slider: _c.SliderBuilder!
        \parent _t.movement
        \name 'Speed Font Size'
        \description 'Set the speed font size used to render speed text.'
        \default _d.font.size.default
        \min _d.font.size.min
        \max _d.font.size.max
        \step _d.font.size.step
        \build!

    speed_font_weight_slider: _c.SliderBuilder!
        \parent _t.movement
        \name 'Speed Font Weight'
        \description 'Set the speed font weight used to render speed text.'
        \default _d.font.weight.default
        \min _d.font.weight.min
        \max _d.font.weight.max
        \step _d.font.weight.step
        \build!

    speed_shadow_check: _c.CheckBuilder!
        \parent _t.movement
        \name 'Speed Shadow'
        \description 'Override the global shadow parameters.'
        \build!

    speed_shadow_offset_x_slider: _c.SliderBuilder!
        \parent _t.movement
        \name 'Speed Shadow Offset X-Axis'
        \description 'Set the speed shadow offset on the x-axis.'
        \default _d.shadow.offset.x
        \min _d.shadow.offset.min
        \max _d.shadow.offset.max
        \step _d.shadow.offset.step
        \build!

    speed_shadow_offset_y_slider: _c.SliderBuilder!
        \parent _t.movement
        \name 'Speed Shadow Offset Y-Axis'
        \description 'Set the speed shadow offset on the y-axis.'
        \default _d.shadow.offset.y
        \min _d.shadow.offset.min
        \max _d.shadow.offset.max
        \step _d.shadow.offset.step
        \build!

    speed_shadow_color: _c.ColorBuilder!
        \parent _t.movement
        \name 'Speed Shadow Color'
        \default _d.color.shadow
        \build!

    speed_shadow_saturation_slider: _c.SliderBuilder!
        \parent _t.movement
        \name 'Speed Shadow Color Saturation'
        \description 'Set the speed shadow color saturation.'
        \default _d.color.saturation.default
        \min _d.color.saturation.min
        \max _d.color.saturation.max
        \step _d.color.saturation.step
        \build!

    speed_shadow_brightness_slider: _c.SliderBuilder!
        \parent _t.movement
        \name 'Speed Shadow Color Brightness'
        \description 'Set the speed shadow color brightness.'
        \default _d.color.brightness.default
        \min _d.color.brightness.min
        \max _d.color.brightness.max
        \step _d.color.brightness.step
        \build!



local _s =
    player: 'CCSPlayer'



local _p =
    otrg: 'm_hObserverTarget'
    scope: 'm_bIsScoped'
    flags: 'm_fFlags'
    rmode: 'm_nRenderMode'
    velx: 'm_vecVelocity[0]'
    vely: 'm_vecVelocity[1]'
    velz: 'm_vecVelocity[2]'



local _l =
    move: 'CreateMove'
    pre_move: 'PreMove'
    draw: 'Draw'
    unload: 'Unload'



local _e =
    lcl: nil
    plr: nil
    trg: nil



local _f = {}



local _n =
    lcllad: 2304
    trglad: 2314



local _fl =
    on_ground: 1
    ducking: 2
    water_jump: 8
    in_water: 1024
    swim: 4096



local _bt =
    in_jump: 2
    in_duck: 4



local _jt =
    none: 0
    jump: 1
    perfect: 2
    jb: 3
    laj: 4
    lag: 5
    lah: 6

_jt[_jt.none] = 'None'
_jt[_jt.jump] = 'Jump'
_jt[_jt.perfect] = 'Perfect'
_jt[_jt.jb] = 'Jump-Bug'
_jt[_jt.laj] = 'Ladder Jump'
_jt[_jt.lag] = 'Ladder Glide'
_jt[_jt.lah] = 'Ladder Hop'



local _x =
    screen:
        w: nil
        h: nil

    specs: nil
    scope: nil
    jt: _jt.none

    cvel: nil
    pvel: nil
    ppvel: nil

    cground: 0
    pground: 0
    cduck: 0
    pduck: 0
    cladder: 0
    pladder: 0
    clag: 0
    plag: 0

    pre_flags: nil
    pre_duck: nil
    pre_ground: nil



callbacks.Register _l.move, (cmd) ->
    _e.lcl = entities.GetLocalPlayer!
    if _e.lcl
        if _e.lcl\IsAlive!
            _e.plr = _e.lcl
            _e.trg = nil
        else
            _e.trg = _e.lcl\GetPropEntity _p.otrg
            _e.plr = _e.trg
    else
        _e.plr = nil
        _e.trg = nil

    if _e.plr and _e.plr\IsAlive!
        do
            _x.specs = {}
            for _, ent in pairs entities.FindByClass _s.player
                if ent\IsAlive!
                    continue
                if not _i.debug_spectators_self_check\GetValue!
                    if ent\GetIndex! == _e.lcl\GetIndex!
                        continue
                target = ent\GetPropEntity _p.otrg
                if not target
                    continue
                if target\GetIndex! ~= _e.plr\GetIndex!
                    continue
                if not _i.debug_spectators_bot_check\GetValue!
                    info = client.GetPlayerInfo ent\GetIndex!
                    if info.IsBot or info.IsGOTV
                        continue
                _x.specs[] = ent

        do
            flags = _e.plr\GetPropInt _p.flags
            _x.pground = _x.cground
            if flags
                if (bit.band flags, _fl.on_ground) == _fl.on_ground
                    if _x.cground < 0
                        _x.cground = 0
                    _x.cground += 1
                else
                    if _x.cground > 0
                        _x.cground = 0
                    _x.cground -= 1
            else
                _x.cground = 0

        do
            flags = _e.plr\GetPropInt _p.flags
            _x.pduck = _x.cduck
            if flags
                if (bit.band flags, _fl.ducking) == _fl.ducking
                    if _x.cduck < 0
                        _x.cduck = 0
                    _x.cduck += 1
                else
                    if _x.cduck > 0
                        _x.cduck = 0
                    _x.cduck -= 1
            else
                _x.cdcuk = 0

        do
            rmode = _e.plr\GetPropInt _p.rmode
            _x.pladder = _x.cladder
            if rmode
                if rmode == _n.lcllad or rmode == _n.trglad
                    if _x.cladder < 0
                        _x.cladder = 0
                    _x.cladder += 1
                else
                    if _x.cladder > 0
                        _x.cladder = 0
                    _x.cladder -= 1
            else
                _x.cladder = 0

        do
            _x.ppvel = _x.pvel
            _x.pvel = _x.cvel

            x = _e.plr\GetPropFloat 'localdata', _p.velx
            y = _e.plr\GetPropFloat 'localdata', _p.vely
            z = _e.plr\GetPropFloat 'localdata', _p.velz
            if not x and not y and not z
                _x.cvel = nil
            else
                if not x
                    x = 0
                if not y
                    y = 0
                if not z
                    z = 0
                _x.cvel = [x, y, z]

        do
            _x.scope = _e.plr\GetPropBool _p.scope
    else
        _x.specs = {}
        _x.cground = 0
        _x.pground = 0
        _x.cduck = 0
        _x.pduck = 0
        _x.cladder = 0
        _x.pladder = 0
        _x.cvel = nil
        _x.pvel = nil
        _x.ppvel = nil
        _x.scope = false

    if _e.lcl and _e.lcl\IsAlive!
        _x.plag = _x.clag
        if _x.cladder ~= 0
            if _x.cladder > 0 and (bit.band cmd.buttons, _bt.in_jump) == _bt.in_jump
                if _x.clag < 0
                    _x.clag = 0
                _x.clag += 1
            else
                if _x.clag > 0
                    _x.clag = 0
                _x.clag -= 1
        else
            _x.clag = 0
    else
        _x.clag = 0
        _x.plag = 0

    if _e.plr and _e.plr\IsAlive!
        flags = _e.plr\GetPropInt _p.flags
        is_ignored_fl = flags and ((bit.band flags, _fl.water_jump) == _fl.water_jump or (bit.band flags, _fl.swim) == _fl.swim or (bit.band flags, _fl.in_water) == _fl.in_water)
        is_ignored_mt = _x.cladder > 0 and is_ignored_fl

        is_ladder_jumped = _x.cladder < 0 and _x.pladder > 0
        is_ladder_glided = _x.clag < 0  and _x.plag > 0
        is_ladder_hopped = is_ladder_jumped and _x.cground == -1 and _x.pground > 0
        is_jump_bugged = _x.pre_duck and _x.cduck < 0 and _x.cground < 0 and ((_x.pvel and _x.pvel[3] < 0) or (_x.pvel and _x.pvel[3] == 0 and _x.ppvel and _x.ppvel[3] < 0)) and _x.cvel and _x.cvel[3] > 0 and not is_ignored_mt

        if _x.cground > 1 or is_ignored_fl
            _x.jt = _jt.none
        elseif is_ladder_hopped
            _x.jt = _jt.lah
        elseif is_ladder_glided
            _x.jt = _jt.lag
        elseif is_ladder_jumped
            _x.jt = _jt.laj
        elseif is_jump_bugged
            _x.jt = _jt.jb
        elseif _x.cground == -1 and _x.cladder < 0
            if _x.pground == 1 or (_x.pground == 2 or _x.pground == 3) and _x.pduck == 1 and _x.cduck == 2 and _x.pre_duck and _x.pre_ground
                _x.jt = _jt.perfect
            else
                _x.jt = _jt.jump
    else
        _x.jt = _jt.none



callbacks.Register _l.pre_move, (cmd) ->
    if _e.plr and _e.plr\IsAlive!
        _x.pre_flags = _e.plr\GetPropInt _p.flags
        _x.pre_duck = (bit.band _x.pre_flags, _fl.ducking) == _fl.ducking
        _x.pre_ground = (bit.band _x.pre_flags, _fl.on_ground) == _fl.on_ground
    else
        _x.pre_flags = nil
        _x.pre_duck = nil
        _x.pre_ground = nil



callbacks.Register _l.draw, ->
    _x.screen.w, _x.screen.h = draw.GetScreenSize!
    if not _x.screen.w or not _x.screen.h
        return

    if _i.debug_check\GetValue!
        fn, fs, fw = nil, nil, nil
        if _i.debug_font_check\GetValue!
            fn = _d.font.names[_i.debug_font_name_combo\GetValue! + 1]
            fs = _i.debug_font_size_slider\GetValue!
            fw = _i.debug_font_weight_slider\GetValue!
        elseif _i.global_font_check\GetValue!
            fn = _d.font.names[_i.global_font_name_combo\GetValue! + 1]
            fs = _i.global_font_size_slider\GetValue!
            fw = _i.global_font_weight_slider\GetValue!

        if fn and fs and fw
            font = _u.fnext(_f, fn, fs, fw)
            if font
                sc, sx, sy, ss, sb = nil, nil, nil, nil, nil
                if _i.debug_shadow_check\GetValue!
                    sc = {_i.debug_shadow_color\GetValue!}
                    sx = _i.debug_shadow_offset_x_slider\GetValue!
                    sy = _i.debug_shadow_offset_y_slider\GetValue!
                    ss = _i.debug_shadow_saturation_slider\GetValue!
                    sb = _i.debug_shadow_brightness_slider\GetValue!
                elseif _i.global_shadow_check\GetValue!
                    sc = {_i.global_shadow_color\GetValue!}
                    sx = _i.global_shadow_offset_x_slider\GetValue!
                    sy = _i.global_shadow_offset_y_slider\GetValue!
                    ss = _i.global_shadow_saturation_slider\GetValue!
                    sb = _i.global_shadow_brightness_slider\GetValue!

                if sc and ss and sb
                    do
                        h, s, b, a = _u.rgb2hsb unpack sc
                        sc = {_u.hsb2rgb h, s * ss, b * sb, a}

                draw.SetFont font

                dc = {_i.debug_color\GetValue!}
                ds = _i.debug_saturation_slider\GetValue!
                db = _i.debug_brightness_slider\GetValue!
                if dc and ds and db
                    do
                        h, s, b, a = _u.rgb2hsb unpack dc
                        dc = {_u.hsb2rgb h, s * ds, b * db, a}

                if _i.debug_spectators_check\GetValue!
                    space_w, space_h = draw.GetTextSize ' '
                    curr_x, curr_y = space_w, space_h
                    for _, ent in ipairs _x.specs
                        text = ent\GetName!

                        if sc and sx and sy
                            draw.Color unpack sc
                            draw.Text curr_x + sx, curr_y + sy, text

                        draw.Color unpack dc
                        draw.Text curr_x, curr_y, text

                        text_w, text_h = draw.GetTextSize text
                        curr_y += text_h + space_h

                if _i.debug_jt_check\GetValue!
                    space_w, space_h = draw.GetTextSize ' '
                    text = _jt[_x.jt]
                    text_w, text_h = draw.GetTextSize text
                    text_x, text_y = _x.screen.w - text_w - space_w, space_h

                    if sc and sx and sy
                        draw.Color unpack sc
                        draw.Text text_x + sx, text_y + sy, text

                    draw.Color unpack dc
                    draw.Text text_x, text_y, text

    if _i.speed_check\GetValue! and (not _i.speed_hide_check\GetValue! or not _x.scope) and _x.cvel
        fn, fs, fw = nil, nil, nil
        if _i.speed_font_check\GetValue!
            fn = _d.font.names[_i.speed_font_name_combo\GetValue! + 1]
            fs = _i.speed_font_size_slider\GetValue!
            fw = _i.speed_font_weight_slider\GetValue!
        elseif _i.global_font_check\GetValue!
            fn = _d.font.names[_i.global_font_name_combo\GetValue! + 1]
            fs = _i.global_font_size_slider\GetValue!
            fw = _i.global_font_weight_slider\GetValue!

        if fn and fs and fw
            font = _u.fnext(_f, fn, fs, fw)
            if font
                sc, sx, sy, ss, sb = nil, nil, nil, nil, nil
                if _i.speed_shadow_check\GetValue!
                    sc = {_i.speed_shadow_color\GetValue!}
                    sx = _i.speed_shadow_offset_x_slider\GetValue!
                    sy = _i.speed_shadow_offset_y_slider\GetValue!
                    ss = _i.speed_shadow_saturation_slider\GetValue!
                    sb = _i.speed_shadow_brightness_slider\GetValue!
                elseif _i.global_shadow_check\GetValue!
                    sc = {_i.global_shadow_color\GetValue!}
                    sx = _i.global_shadow_offset_x_slider\GetValue!
                    sy = _i.global_shadow_offset_y_slider\GetValue!
                    ss = _i.global_shadow_saturation_slider\GetValue!
                    sb = _i.global_shadow_brightness_slider\GetValue!

                if sc and ss and sb
                    do
                        h, s, b, a = _u.rgb2hsb unpack sc
                        sc = {_u.hsb2rgb h, s * ss, b * sb, a}

                draw.SetFont font

                spc = {_i.speed_color\GetValue!}
                sps = _i.speed_saturation_slider\GetValue!
                spb = _i.speed_brightness_slider\GetValue!
                if spc and sps and spb
                    do
                        h, s, b, a = _u.rgb2hsb unpack spc
                        spc = {_u.hsb2rgb h, s * sps, b * spb, a}

                x, y, z = nil, nil, nil
                if _i.speed_ladder_check\GetValue! and _x.cladder > 0 and _x.cground < 0 and _x.clag < 0
                    x, y, z = 0, 0, _x.cvel[3]
                else
                    x, y, z = _x.cvel[1], _x.cvel[2], _x.cvel[3]
                    if not _i.speed_x_check\GetValue!
                        x = 0
                    if not _i.speed_y_check\GetValue!
                        y = 0
                    if not _i.speed_z_check\GetValue!
                        z = 0

                accuracy = _i.speed_acc_slider\GetValue!
                zeros = _i.speed_zeros_slider\GetValue!
                if accuracy ~= 0
                    zeros += accuracy + 1

                text = string.format '%0' .. (tostring zeros) .. '.' .. (tostring accuracy) .. 'f', _u.vlen3 x, y, z
                text_w, text_h = draw.GetTextSize text
                text_x, text_y = _x.screen.w * _i.speed_x_pos_slider\GetValue! - text_w / 2, _x.screen.h * (1 - _i.speed_y_pos_slider\GetValue!) - text_h / 2

                if sc and sx and sy
                    draw.Color unpack sc
                    draw.Text text_x + sx, text_y + sy, text

                draw.Color unpack spc
                draw.Text text_x, text_y, text
